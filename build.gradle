/**
 * http://central.sonatype.org/pages/gradle.html
 */

apply plugin: 'groovy'
apply plugin: 'project-report'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'

sourceCompatibility = 1.5
group = 'me.andrz.findbugs'
version = '0.1.0'

// Define these so CI build doesn't fail, but add to "$GRADLE_USER_HOME/gradle.properties".
ext.ossrhUsername = null
ext.ossrhPassword = null

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
    distributionUrl = "http://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'org.slf4j:slf4j-simple:1.7.10'
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.google.guava:guava:18.0'
//    compile 'com.google.code.findbugs:findbugs:3.0.0'
//    compile 'com.google.code.findbugs:findbugs-ant:3.0.0'
//    compile 'org.apache.ant:ant:1.9.4'
    testCompile 'junit:junit:4.11'
}

tasks.withType(Test) {
    systemProperties = System.getProperties()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'me.andrz.findbugs.diff.FindBugsDiffCli',
        )
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

if (System.getenv('CI') != 'true') {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'findbugs-diff'
                packaging 'jar'
                artifactId 'findbugs-diff'
                description 'Report FindBugs diffs.'
                url 'https://github.com/AndersDJohnson/findbugs-diff'

                scm {
                    connection 'scm:git:https://github.com/AndersDJohnson/findbugs-diff.git'
                    developerConnection 'scm:git:git@github.com:AndersDJohnson/findbugs-diff.git'
                    url 'https://github.com/AndersDJohnson/findbugs-diff'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'AndersDJohnson'
                        name 'Anders D. Johnson'
                        url 'https://andrz.me'
                        email 'anders.d.johnson.developer@gmail.com'
                    }
                }
            }
        }
    }
}
